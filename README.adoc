=== Step 1: Setup project

NOTE: Ensure that your FLEX_HOME environment variable points to a valid Apache Flex installation directory before starting Eclipse / GGTS

Create a new project with "File / New Grails Project" or 

----
grails create-app democustomer

cd democustomer
----

Temporary : add snapshot repository in +conf/BuildConfig.groovy+ until GraniteDS 3.1.1.GA and the Grails plugin are released as final versions. 

----
repositories {
  ...
  mavenRepo "https://oss.sonatype.org/content/repositories/snapshots/"
}
----

Add plugin dependency in +grails-app/conf/BuildConfig.groovy+

----
plugins {
  ...
  compile 'org.graniteds.grails:gdsflex:2.0.0-SNAPSHOT'
}
----

Resolve dependencies and install gdsflex plugin with right-click on project democustomer / Grails Tools / Refresh Dependencies or

----
grails compile
----

=== Step 2: Domain model

Create a domain class named Customer with File / New Domain Class or

----
grails create-domain-class Customer
----

.grails-app/domain/democustomer/Customer.groovy
[source,groovy]
----
package democustomer

class Customer implements java.io.Serializable {

    static constraints = {
	}
	
	String uid
	
	String firstName
	
	String lastName
	
	Integer numberOfLogons = 0
	
	// This is to ensure that the entity will have a proper unique uid
	// before being persisted
	def beforeValidate() {
		if (uid == null)
			uid = java.util.UUID.randomUUID().toString();
			
		if (numberOfLogons == null)
			numberOfLogons = 0;
	}
}
----

NOTE: The +beforeValidate+ method ensures proper initialization of default values for object properties in case
we get null values from the Flex client


We are going to create some initial example data in the +grails-app/conf/BootStrap.groovy+ file

[source,groovy]
----
import democustomer.Customer

class BootStrap {

    def init = { servletContext ->
		
		println "Init db"
        new Customer(firstName: "Justin M.", lastName: "Hill").save(flush:true);
        new Customer(firstName: "Pan", lastName: "Li").save(flush:true);
    }
	
    def destroy = {
    }
}
----


=== Step 3: Service

Create a service named CustomerService with File / New Service or

----
grails create-service Customer
----

We are going to build a very simple transactional service to execute database operations on the Customer entity.

[source,groovy]
----
package democustomer

import org.granite.tide.annotations.TideEnabled;
import org.springframework.transaction.annotation.Transactional;

@TideEnabled	// <1>
@Transactional
class CustomerService {
	
	def list(Object filter, int first, int max, String[] order, boolean[] desc) {	// <2>
		if (max <= 0)
			max = 36
				
		def resultList = (order && order.length > 0) 
			? Customer.list(sort: order[0], order: desc[0] ? "desc" : "asc") 
			: Customer.list()
		
		def resultCount = Customer.count()
		
		[ resultList: resultList, resultCount: resultCount, firstResult: first, maxResults: max ]
    }
    
    def find(Object id) {
    	def customer = Customer.get(id)
    	return customer
    }

	def save(Object customer) {
		customer = customer.merge(flush:true)	// <3>
		return customer
	}
	
	def remove(Object customer) {
		def customer = Customer.get(customer.id)
		if (customer)
			customer.delete()
	}
}
----

<1> +@TideEnabled+ indicates that the service is accessible from Flex
<2> The method +list+ has a specific signature and returns a +Map+ with specified entries +resultList+, +resultCount+, +firstResult+, +maxResults+ so it can be used from the Flex +PagedQuery+ component
<3> We use the +customer.merge+ instead of +customer.save+ because usually the object customer will be a detached object received from Flex 


=== Step 4: Create a simple Flex UI

First generate the AS3 domain model:

----
grails gas3
----

This will launch the AS3 class generator and generate AS3 domain classes in +grails-app/views/flex+.


Then in +grails-app/views/flex+ we will create a very simple application:

.grails-app/views/flex/democustomer.mxml
----
<?xml version="1.0" encoding="utf-8"?>

<s:Application
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:mx="library://ns.adobe.com/flex/mx" 
	xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns="*"
    preinitialize="init()">
   
    <fx:Script>
        <![CDATA[
			import democustomer.Customer;
			import democustomer.CustomerService;
			
			import mx.collections.ListCollectionView;
			import mx.core.Container;
			
			import org.granite.tide.service.SimpleServerApp;
			import org.granite.tide.spring.Context;
			import org.granite.tide.spring.PagedQuery;
			import org.granite.tide.spring.Spring;
			
			
            private function init():void {
            	Spring.getInstance().initApplication();
            	
				Spring.getInstance().mainServerSession.serverApp = 				// <1>
					new SimpleServerApp("/democustomer", false, "localhost", "8080");
				
				// Register a PagedQuery component to display the list of customers
				Spring.getInstance().addComponentWithFactory("customerList", PagedQuery,		// <2> 
					{ maxResults: 36, methodName: "list", remoteComponentClass: CustomerService, elementClass: Customer });
			}
        ]]>
    </fx:Script>
	
	<MainView id="mainView" width="100%" height="100%"/>
</s:Application>
----
<1> Configures the server parameters in case the application is launched in a standalone Flash player (IDE debug mode for example). It's not necessary when executed inside the browser.
<2> Configures the +PagedQuery+ component than handles paging and sorting on the data requested from the remote service. 

And the main view, which is basically a +DataGrid+ using the +PagedQuery+ as data provider:

.grails-app/views/flex/MainView.mxml
----
<?xml version="1.0" encoding="utf-8"?>

<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  xmlns:s="library://ns.adobe.com/flex/spark">
	
	<fx:Metadata>[Name]</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import democustomer.Customer;
			import democustomer.CustomerService;
			
			import mx.data.utils.Managed;
			
			import org.granite.tide.spring.PagedQuery;
			
			[Bindable] [In]
			public var customerList:PagedQuery;
			
			[Bindable] [Out]
			public var customer:Customer = new Customer();
			
			[Inject]
			public var customerService:CustomerService;
			
			
			private function create():void {
				cancel();
				customer = new Customer();
			}
			
			private function remove():void {
				customerService.remove(customer.id);
			}
			
			private function save():void {
				customerService.save(customer);
			}
			
			private function cancel():void {
				Managed.resetEntity(customer);
			}
			
			private function select():void {
				cancel();
				if (customerUI.selectedItem != null)
					customer = Customer(customerUI.selectedItem);
				else
					customer = new Customer();
			}
		]]>
	</fx:Script>
	
	<s:VGroup width="100%" paddingLeft="10" paddingRight="10" paddingTop="15" paddingBottom="15">
		<s:Label text="GraniteDS / Grails example application" fontSize="24" fontWeight="bold" color="black"/>
	</s:VGroup>
	
	<s:HGroup width="100%" height="100%" paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10" gap="10">
		<s:DataGrid id="customerUI" width="100%" height="100%" dataProvider="{customerList}"
					valueCommit="select()">
			<s:columns>
				<s:ArrayList>
					<s:GridColumn headerText="Id" dataField="id"/>
					<s:GridColumn headerText="First name" dataField="firstName"/>
					<s:GridColumn headerText="Last name" dataField="lastName"/>
					<s:GridColumn headerText="Number of logons" dataField="numberOfLogons"/>
				</s:ArrayList>
			</s:columns>
		</s:DataGrid>
		
		<s:VGroup>						
			<s:HGroup gap="10">
				<s:Button id="bCreate" label="Create" click="create()"/>
				
				<s:Button id="bDelete" label="Delete" enabled="{!isNaN(customer.version)}" click="remove()"/>
			</s:HGroup>
			
			<s:Form>
				<s:FormHeading label="{isNaN(customer.version) ? 'New customer' : 'Edit customer'}"/>
				
				<s:FormItem label="First name">
					<s:TextInput id="iFirstName" text="@{customer.firstName}"/>
				</s:FormItem>
				<s:FormItem label="Last name">
					<s:TextInput id="iLastName" text="@{customer.lastName}"/>
				</s:FormItem>
			</s:Form>
			
			<s:HGroup gap="10">
				<s:Button id="bSave" label="Save" enabled="{customer.meta_dirty}" click="save()"/>
				
				<s:Button id="bCancel" label="Cancel" enabled="{customer.meta_dirty}" click="cancel()"/>
			</s:HGroup>
		</s:VGroup>
	</s:HGroup>
</s:VGroup>
----

If the application is not compiled by the IDE, you can compile the application with:

----
grails mxmlc
----

Then start the server:

----
grails run-app
----

And browse the application at http://localhost:8080/democustomer/democustomer.swf

At this point you should be able to create/update/delete customers.


=== Step 5: Configure Spring Security

First install the Spring Security plugin in grails-app/conf/BuildConfig.groovy

----
plugins {
  ...
  compile ':spring-security-core:2.0-RC4'
  runtime 'org.graniteds.grails:spring-security-acl:2.0-SNAPSHOT'
}
----

NOTE: We will use a forked version of the Spring Security ACL plugin which is updated to be compatible with Grails 2.4.3.

And install the plugin with project democustomer / Right click / Grails Tools / Refresh Dependencies or

----
grails refresh-dependencies
----

Configure the plugin, first security domain classes

----
grails s2-quickstart democustomer Person Authority
----

Next permit outside access to GraniteDS urls in +grails-app/conf/Config.groovy+

----
grails.plugin.springsecurity.controllerAnnotations.staticRules = [
	'/':                              ['permitAll'],
	'/*.swf':						  ['permitAll'],
	'/graniteamf/**':				  ['permitAll'],
	'/gravityamf/**':				  ['permitAll'],
	'/index':                         ['permitAll'],
	'/index.gsp':                     ['permitAll'],
	'/assets/**':                     ['permitAll'],
	'/**/js/**':                      ['permitAll'],
	'/**/css/**':                     ['permitAll'],
	'/**/images/**':                  ['permitAll'],
	'/**/favicon.ico':                ['permitAll']
]
----

And finally create initial users in +grails-app/conf/BootStrap.groovy+

[source,groovy]
----
...
import democustomer.Person
import democustomer.Authority
import democustomer.PersonAuthority

class BootStrap {
	
    def init = { servletContext ->
		
		...		
		def user_admin = new Person(username:"admin", password:"admin", enabled:true).save(flush:true)
		
		def user_user = new Person(username:"user", password:"user", enabled:true).save(flush:true)
		
		def role_admin = new Authority(description:"Admin", authority:"ROLE_ADMIN").save(flush:true)
		def role_user = new Authority(description:"User", authority:"ROLE_USER").save(flush:true)
				  
		new PersonAuthority(person:user_admin, authority:role_admin).save(flush:true)
		new PersonAuthority(person:user_admin, authority:role_user).save(flush:true)
		new PersonAuthority(person:user_user, authority:role_user).save(flush:true)
    }
	
    def destroy = {
    }
}
----

Now that the plugin is correctly configured, we can secure our service so it can be accessed only by authenticated 
users having the role ROLE_USER

----
package democustomer

import org.granite.tide.annotations.TideEnabled;
import org.springframework.transaction.annotation.Transactional;
import grails.plugin.springsecurity.annotation.Secured

@TideEnabled
@Transactional
@Secured(['ROLE_USER'])
class CustomerService {
	...
}
----

And add a login page in the main Flex application in +grails-app/views/flex/democustomer.mxml+:

[source,actionscript]
.grails-app/views/flex/democustomer.mxml
----
<?xml version="1.0" encoding="utf-8"?>

<s:Application
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:mx="library://ns.adobe.com/flex/mx" 
	xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns="*"
    preinitialize="Spring.getInstance().initApplication()"
    initialize="init()">
   
    <fx:Script>
        <![CDATA[
        	...
			import org.granite.tide.spring.Identity;
            
			
            [Bindable] [Inject]
    		public var identity:Identity;
            
			
            private function init():void {
            	...
			}
        ]]>
    </fx:Script>
	
	<mx:ViewStack id="appView" selectedIndex="{identity.loggedIn ? 1 : 0}" width="100%" height="100%">
		<s:NavigatorContent>
			<LoginView id="loginView" width="100%" height="100%"/>
		</s:NavigatorContent>
       	
		<s:NavigatorContent>
			<MainView id="mainView" width="100%" height="100%"/>
		</s:NavigatorContent>
	</mx:ViewStack>
</s:Application>
----


[source,actionscript]
----
<?xml version="1.0" encoding="utf-8"?>

<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
  	xmlns:mx="library://ns.adobe.com/flex/mx" 
  	xmlns:s="library://ns.adobe.com/flex/spark"
	width="100%" height="100%" verticalAlign="middle" horizontalAlign="center">
	
	<fx:Metadata>[Name]</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import org.granite.tide.events.TideFaultEvent;
			import org.granite.tide.events.TideResultEvent;
			import org.granite.tide.spring.Identity;
			
			[Bindable] [Inject]
			public var identity:Identity;
			
			[Bindable]
			private var message:String;
			
			private function loginResult(event:TideResultEvent):void {
				message = "";
			}
			
			private function loginFault(event:TideFaultEvent):void {
				message = event.fault.faultString;
			}
		]]>
	</fx:Script>
	
	<s:Panel title="Login">
		<s:layout>
			<s:VerticalLayout horizontalAlign="center" gap="0" paddingTop="8" paddingBottom="8"/>
		</s:layout>
		
		<s:Form>
			<s:FormItem label="Username">
				<s:TextInput id="username"/>
			</s:FormItem>
			<s:FormItem label="Password">
				<s:TextInput id="password" displayAsPassword="true"
							  enter="identity.login(username.text, password.text, loginResult, loginFault);"/>
			</s:FormItem>
		</s:Form>
		
		<s:Label text="{message}"/>
		
		<s:Button label="Login"
				   click="identity.login(username.text, password.text, loginResult, loginFault);"/>
	</s:Panel>
</s:VGroup>
----

=== Step 6: Configure GraniteDS Data Push

Using data push first requires to define a messaging destination in +conf/spring/resources.groovy+.
It will be subscribed by the Flex clients and used by the server to push data updates.

----
beans = {
	xmlns graniteds:"http://www.graniteds.org/config"
	graniteds."messaging-destination"('id': 'customerTopic', "no-local": true, 'session-selector': true)
}
----

As it will be used by the server to publish data updates, it has to be subscribed by the clients

----
<mx:Script>
	<![CDATA[
		...
		import org.granite.tide.data.DataObserver;
		import org.granite.tide.data.ChangeMerger;

		...
		private function init():void {
			...
			// Register a data observer component with the name of the topic
			Spring.getInstance().addComponent("customerTopic", DataObserver, false, true);
			// Register a Change Set merger to handle incremental updates
			Spring.getInstance().addComponents([ChangeMerger]);
			// Binds the subscribe and unsubscribe methods of the component to the application login/logout events
			Spring.getInstance().addEventObserver("org.granite.tide.login", "customerTopic", "subscribe");
			Spring.getInstance().addEventObserver("org.granite.tide.logout", "customerTopic", "unsubscribe");
			...
		}
----

Now we have to tell Grails and GraniteDS that updates made through our customer service should be published by annotating
the service with @DataEnabled

----
package democustomer

import org.granite.tide.annotations.TideEnabled;
import org.springframework.transaction.annotation.Transactional;
import grails.plugin.springsecurity.annotation.Secured
import org.granite.tide.data.DataEnabled;

@TideEnabled
@Transactional
@Secured(['ROLE_USER'])
@DataEnabled(topic="customerTopic", publish=DataEnabled.PublishMode.ON_SUCCESS)
class CustomerService {
	...
}
----

Finally we have to plug GraniteDS into Hibernate to be able to detect and publish the changes on entities by 
adding a file META-INF/services/org.hibernate.integrator.spi.Integrator containing the following line :

----
org.granite.tide.hibernate4.Hibernate4ChangeSetIntegrator
----

Now restart everything, and if you open two browsers on the application, you can see changes made in one browser 
applied in real-time on the other.


=== Step 7: Add server-generated pushed data

Until now all updates were made by users through the UI. 
Now we are going to simulate server-side data changes with a service generating random logons on users.

First the service

----
package democustomer

import org.granite.tide.data.DataEnabled;
import org.springframework.transaction.annotation.Transactional;

@DataEnabled(topic="customerTopic", publish=DataEnabled.PublishMode.ON_COMMIT, useInterceptor=true)
class RandomLogonGeneratorService {

    def generateLogon() {
		int count = Customer.count()
		if (count == 0)
			return;
		
		int id = new java.util.Random().nextInt(count) + 1;
		
		Customer customer = Customer.get(id);
		if (customer != null) {
			println "Update customer " + customer.id + " (version " + customer.version + ")"
			customer.numberOfLogons++;
			customer.save(flush: true);
		}
    }
}
----

There is nothing special in this service which just selects a random Customer in the database, and increases
its numberOfLogons property.
The only thing is the annotation @DataEnabled (similar to the one we have used on the service) that allows
GraniteDS to intercept the execution of the service and publish the data changes. The main difference here is the
useInterceptor=true attribute which is necessary because this service is executed out of the scope of a GraniteDS
remoting request. GraniteDS thus has to use a Spring AOP interceptor to do its work.

Finally we schedule the execution of this service at regular periods in BootStrap.groovy

----
...
import java.util.concurrent.ScheduledThreadPoolExecutor;
import java.util.concurrent.TimeUnit
import java.util.concurrent.Callable


class BootStrap {
	
	def randomLogonGeneratorService
	
	def persistenceInterceptor
	

    def init = { servletContext ->
		...
		println "Start logon generator"
		ScheduledThreadPoolExecutor scheduledExecutor = new ScheduledThreadPoolExecutor(1);
		scheduledExecutor.scheduleAtFixedRate({
			persistenceInterceptor.init()
			try {
				randomLogonGeneratorService.generateLogon()
			} 
			finally {
				persistenceInterceptor.flush()
				persistenceInterceptor.destroy()
			}
		} as Runnable, 10, 10, TimeUnit.SECONDS)
	}
	
	...
}
----

The +persistenceInterceptor+ is necessary to setup and cleanup the GORM environment in the scheduler thread.
This scheduled executor is just here for the simulation, in real-world cases the changes would be generated by 
calls on the service made by external servers.

We are also going to add a little status message to indicate what updates the user has received :

[source,actionscript]
----
	private var timer:Timer;
	
	[Observer("org.granite.tide.data.update.Customer")]
	public function showUpdate(event:TideContextEvent):void {
		timer = new Timer(2000);
		timer.addEventListener(TimerEvent.TIMER, clearUpdateMessage);
		timer.start();
		var customer:Customer = Customer(event.params[0]);
		lMessage.text = "Update received: " + customer.firstName + " " + customer.lastName;
	}
	
	private function clearUpdateMessage(event:TimerEvent):void {
		timer.removeEventListener(TimerEvent.TIMER, clearUpdateMessage);
		lMessage.text = "";
	}
---- 

The +[Observer]+ annotation indicates that the method should be called whenever the client receives a data update event for a +Customer+ object.
+event.params[0]+ then contains the updated object. We set a timer to clear the message after 2 seconds.

If you restart everything, the column numberOfLogons should be updated in the Flex clients every 10s without 
any manual user intervention, and the status message indicates which update is received.


=== Step 8: Add by-company isolation security

We would like that customers and users be isolated from others depending on which company they belong to. 
Thus users from a company should see only customers from the same company.

First we have to create a new domain class +Company+ which for now will just have a name and id.

[source,groovy]
----
package democustomer

class Company implements java.io.Serializable {

    static constraints = {
	}
	
	String uid
	
	String name
	
	def beforeValidate() {
		if (uid == null)
			uid = java.util.UUID.randomUUID().toString();
	}
}
----

And use it in our other domain classes to reference their owning company:

[source,groovy]
----
class Person {

	transient springSecurityService

	String username
	String password
	boolean enabled = true
	boolean accountExpired
	boolean accountLocked
	boolean passwordExpired
	
	Company company
	
	...
}
----

[source,groovy]
----
class Customer implements CompanyOwned, java.io.Serializable {

    static constraints = {
	}
	
	String uid
	
	String firstName
	
	String lastName
	
	Company company
	
	...
}
----

Note that we implement a simple interface CompanyOwned that we will use later to determine which classes should be filtered 
by company.

As we have changed the domain classes, we have to regenerate the AS3 classes with:

----
grails gas3
---- 

Next we change the set of example users so they are isolated in two different companies:

[source,groovy]
.conf/BootStrap.groovy
----
		println "Init users"
		def company_example1 = new Company(name:"example1").save(flush:true)
		def company_example2 = new Company(name:"example2").save(flush:true)
		
		def user_admin = new Person(company:company_example1, username:"admin", password:"admin", enabled:true).save(flush:true)
		
		def user_user = new Person(company:company_example1, username:"user", password:"user", enabled:true).save(flush:true)
		
		def user_norole = new Person(company:company_example1, username:"norole", password:"norole", enabled:true).save(flush:true)

		def user_user2 = new Person(company:company_example2, username:"user2", password:"user2", enabled:true).save(flush:true)
		
		def role_admin = new Authority(description:"Admin", authority:"ROLE_ADMIN").save(flush:true)
		def role_user = new Authority(description:"User", authority:"ROLE_USER").save(flush:true)
		
		new PersonAuthority(person:user_admin, authority:role_admin).save(flush:true)
		new PersonAuthority(person:user_admin, authority:role_user).save(flush:true)
		new PersonAuthority(person:user_user, authority:role_user).save(flush:true)
		new PersonAuthority(person:user_user2, authority:role_user).save(flush:true)
		
		println "Init db"
        new Customer(company:company_example1, firstName: "Justin", lastName: "Hill").save(flush:true);
        new Customer(company:company_example1, firstName: "Pan", lastName: "Li").save(flush:true);
		new Customer(company:company_example2, firstName: "William", lastName: "Drai").save(flush:true);
----

Next step is to expose this new property to the Spring security authentication system, so we create a custom user details implementation extending +GrailsUser+:

[source,groovy]
.src/groovy/democustomer/CompanyUser.groovy
----
package democustomer

import org.springframework.security.core.GrantedAuthority;

import grails.plugin.springsecurity.userdetails.GrailsUser;

class CompanyUser extends GrailsUser {
	
	final Company company
	
	CompanyUser(String username, String password, boolean enabled, boolean accountNonExpired, boolean credentialsNonExpired,
		boolean accountNonLocked, Collection<GrantedAuthority> authorities, long id, Company company) {
		
		super(username, password, enabled, accountNonExpired, credentialsNonExpired, accountNonLocked, authorities, id)

		this.company = company
	}
}
----

And use it in a custom user details service extending the default one:

[source,groovy]
.src/groovy/democustomer/CompanyUserDetailsService.groovy
----
package democustomer

import org.springframework.security.core.GrantedAuthority;
import org.springframework.security.core.userdetails.UserDetails;

import grails.plugin.springsecurity.userdetails.GormUserDetailsService;

class CompanyUserDetailsService extends GormUserDetailsService {

	protected UserDetails createUserDetails(Person person, Collection<GrantedAuthority> authorities) {
		
		new CompanyUser(person.username, person.password, person.enabled, !person.accountExpired, !person.passwordExpired,
				!person.accountLocked, authorities, person.id, person.company)
	}
}
----

And finally register the service in the Grails application context:

[source,groovy]
.grails-app/conf/spring/resources.groovy
----
beans = {
	...	
	userDetailsService(CompanyUserDetailsService) {
		grailsApplication = ref('grailsApplication')
	}	
	...
}
----

With this configuration we can now access the current user and its company from any part of the application with:

[source,groovy]
----
Company company = SecurityContextHolder.getContext().getAuthentication().getPrincipal().company
----

We are going to use this in +CustomerService+ to filter the objects that are returned depending on the company of the current logged in user:

[source,groovy]
----
def list(Object filter, int first, int max, String[] order, boolean[] desc) {
	if (max <= 0)
		max = 36
	
	Company company = SecurityContextHolder.getContext().getAuthentication().getPrincipal().company
	
	def resultList = (order && order.length > 0) 
		? Customer.findAllByCompany(company, [ offset: first, max: max, sort: order[0], order: desc[0] ? "desc" : "asc" ]) 
		: Customer.findAllByCompany(company, [ offset: first, max: max ])
	
	def resultCount = Customer.countByCompany(company)
	
	[ resultList: resultList, resultCount: resultCount, firstResult: first, maxResults: max ]
}
----

And set the current company when creating new customers:

[source,groovy]
----
def save(Object customer) {
	if (customer.id == null && customer.company == null)
		customer.company = SecurityContextHolder.getContext().getAuthentication().getPrincipal().company
	
	customer = customer.merge(flush:true)
	return customer
}
----

We can improve the security of the service further by using the annotations +@PreAuthorize+ and +@PostAuthorize+ of Spring Security.
To do this we have to implement a custom +PermissionEvaluator+:

[source,groovy]
.src/groovy/democustomer/CompanyPermissionEvaluator.groovy
----
package democustomer

import java.io.Serializable;

import org.springframework.security.access.PermissionEvaluator;
import org.springframework.security.core.Authentication;
import org.springframework.security.core.GrantedAuthority;
import org.springframework.security.core.userdetails.UserDetails;

import grails.plugin.springsecurity.userdetails.GormUserDetailsService;

class CompanyPermissionEvaluator implements PermissionEvaluator {

	@Override
	public boolean hasPermission(Authentication authentication, Object targetDomainObject, Object permission) {
		Company company = authentication.principal.company;		
		if (targetDomainObject instanceof CompanyOwned) {
		 	if (targetDomainObject.companyId != null)
			 	return targetDomainObject.companyId == company.id
			else
				return targetDomainObject.id == null	// null company allowed only for new objects
		}
		return true;
	}
	
	@Override
	public boolean hasPermission(Authentication authentication, Serializable targetId, String targetType, Object permission) {
		return true;
	}
	
}
----

We implement only the first method that we will use in authorization expressions. It just consists in comparing the companies
of the current user and of the protected object if it is a CompanyOwned object.
A real implementation could be more complete and should take care of the type of the requested permission (read, write, ...).

NOTE: We compare targetDomainObject.companyId and not targetDomainObject.company or targetDomainObject.company.id because company will usually 
be a Hibernate proxy and will not necessarily be loaded from the database. Comparing ids ensures that we don't try to initialize the proxy
(which will fail because at the time the evaluation of the permissions the transaction is committed and the Hibernate session is closed).

This permission evaluator allows us to define authorizations at the service level:

[source,groovy]
----
@PostAuthorize("hasPermission(returnObject, read)")
def find(Object id) {
	def customer = Customer.get(id)
	return customer
}

@PreAuthorize("hasPermission(#customer, write)")
def save(Object customer) {
	customer = customer.merge(flush:true)
	return customer
}

@PreAuthorize("hasPermission(#customer, delete)")
def remove(Object customer) {
	customer = Customer.get(customer.id)
	if (customer)
		customer.delete()
}
----

+Post+ and +Pre+ authorizations check that the current user has the correct right to access the object specified in the expression.
For example +hasPermission(returnObject, read)+ ensures that the user can read the object returned by the method.

Now we are sure that the users cannot access data from another company though the service.
However if you look at the status message in the Flex application you can see that the user receives updates for objects of other companies
from server push. This is completely normal as we have not defined any filter on the push service. To do this we have to implement the 
+DataTopicParams+ interface and configure the +@DataEnabled+ annotations to use it:

[source,groovy]
.src/groovy/democustomer/CompanyFilter.groovy
----
package democustomer

import grails.plugin.springsecurity.SpringSecurityUtils;
import grails.plugin.springsecurity.userdetails.GrailsUser;

import org.granite.tide.data.DataObserveParams;
import org.granite.tide.data.DataPublishParams;
import org.granite.tide.data.DataTopicParams;
import org.springframework.security.core.Authentication;
import org.springframework.security.core.context.SecurityContextHolder;

class CompanyFilter implements DataTopicParams {

	@Override
	public void observes(DataObserveParams params) {
		if (SecurityContextHolder.getContext() == null)
			return;
		Authentication authentication = SecurityContextHolder.getContext().getAuthentication()
		if (authentication != null && authentication.principal != null)
			params.addValue("companyId", String.valueOf(authentication.principal.company?.id))
	}
	
	@Override
	public void publishes(DataPublishParams params, Object entity) {
		if (entity instanceof CompanyOwned)
			params.setValue("companyId", entity.company ? String.valueOf(entity.company.id) : "");
	}
}
----

This is relatively similar to the permission resolver. +observes+ is called when a push-enabled method is called and
indicate to GraniteDS what filter parameters it should use to build a server-side selector for the next pushed messages.

Here we build a simple selector depending on the company id of the current user. That means that only the messages
that match the parameter will be received by the user.

The method +publishes+ is called before each message is sent to the user and defines filter headers params for that
particular update message. Here we just set the companyId of the published entity.

Finally we have to set this filter class in the +@DataEnabled+ annotation in all services:

[source,groovy]
----
@TideEnabled
@Transactional
@Secured(['ROLE_USER'])
@DataEnabled(topic="customerTopic", publish=DataEnabled.PublishMode.ON_COMMIT, params=CompanyFilter.class, useInterceptor=true)
class CustomerService {
   ...
}
----

[source,groovy]
----
@DataEnabled(topic="customerTopic", publish=DataEnabled.PublishMode.ON_COMMIT, params=CompanyFilter.class, useInterceptor=true)
class RandomLogonGeneratorService {
   ...
}
----

If you now restart the application, the current user will receive only updates that concern its company and not others.
